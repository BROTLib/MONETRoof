<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.15">
  <POU Name="FB_RoofControl" Id="{d87693f7-b561-4571-8c76-2cffdf8ef5fe}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_RoofControl IMPLEMENTS I_Roof
VAR_INPUT
	min_speed				: UINT;			// minimum speed to drive
	max_speed				: UINT;			// maximum speed to drive
	acceleration			: UINT;			// acceleration [speed/call]
	min_position			: INT;			// minimum position of roof
	max_position			: INT;			// maximum position of roof
	max_position_diff		: INT;			// maximum allowed position difference between two drives of roof
	limit_slowdown			: REAL := 5;	// linear slowdown when moving towards limits starting at <x% and >(100-x)%

	// commands for automatic roof controls
	open_roofs				: BOOL;
	close_roofs				: BOOL;
	stop_roofs				: BOOL;
	reset_roofs				: BOOL;	
	open_roof				: ARRAY[1..2] OF BOOL;
	close_roof				: ARRAY[1..2] OF BOOL;
	stop_roof				: ARRAY[1..2] OF BOOL;
	reset_roof				: ARRAY[1..2] OF BOOL;
	
	// automatic mode
	automatic		AT%I*	: BOOL;
	
	// UPS failure
	ups_fail		AT%I*	: BOOL;
	
	// telemetry interval
	fTelemetryInterval		: TIME := T#5S;
END_VAR
VAR_OUTPUT
	// state
	roof_state				: E_RoofState;
END_VAR
VAR
	// roofs
	roofs					: ARRAY[1..2] OF FB_Roof;
	
	// loop variable	
	i, j					: UINT;
	
	// open and close delays
	tonOpen2Delay			: TON;
	tonClose1Delay			: TON;
	
	// Comm
	fbComm					: I_Comm;	
	tonComm 				: TON := (PT:=fTelemetryInterval);
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[// call roofs
FOR i := 1 TO 2 DO
	// reset
	IF reset_roofs OR reset_roof[i] THEN
		roofs[i].Reset();
	END_IF	

	// automatic or manual mode
	IF automatic THEN
		roofs[i].Automatic(open_roof := open_roofs OR open_roof[i], close_roof := close_roofs OR close_roof[i], stop_roof := stop_roofs OR stop_roof[i]);
	ELSE
		roofs[i].Manual();
	END_IF

	// run 
	roofs[i](		
		roof_number			:= i,
		min_speed			:= min_speed,
		max_speed 			:= max_speed, 
		acceleration 		:= acceleration, 
		min_position 		:= min_position, 
		max_position 		:= max_position,
		max_position_diff 	:= max_position_diff,
		limit_slowdown		:= limit_slowdown,
		comm 				:= fbComm
	);
END_FOR

// set state
IF roofs[1].state = E_RoofState.error OR roofs[2].state = E_RoofState.error THEN
	roof_state := E_RoofState.error;
ELSIF roofs[1].state = E_RoofState.opened AND roofs[2].state = E_RoofState.opened THEN
	roof_state := E_RoofState.opened;
ELSIF roofs[1].state = E_RoofState.closed AND roofs[2].state = E_RoofState.closed THEN
	roof_state := E_RoofState.closed;
ELSIF roofs[1].state = E_RoofState.opening OR roofs[2].state = E_RoofState.opening THEN
	roof_state := E_RoofState.opening;	
ELSIF roofs[1].state = E_RoofState.closing OR roofs[2].state = E_RoofState.closing THEN
	roof_state := E_RoofState.closing;
ELSIF (roofs[1].state = E_RoofState.stopped OR roofs[1].state = E_RoofState.opened OR roofs[1].state = E_RoofState.closed) AND 
      (roofs[2].state = E_RoofState.stopped OR roofs[2].state = E_RoofState.opened OR roofs[2].state = E_RoofState.closed) THEN
	roof_state := E_RoofState.stopped;
ELSE
	roof_state := E_RoofState.error;	
END_IF

// send telemetry
tonComm(IN:=TRUE);
IF tonComm.Q THEN // publish new payload every 5 seconds
	tonComm(IN:=FALSE);
	THIS^._SendTelemetry();	
END_IF
]]></ST>
    </Implementation>
    <Method Name="_SendTelemetry" Id="{35843360-1e80-4635-b873-96034fb47b44}">
      <Declaration><![CDATA[METHOD _SendTelemetry : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF roof_state = E_RoofState.opened THEN
	fbComm.Publish('telescope', 'dome', 'AUXILIARY.DOME.REALPOS', '1.0');
ELSIF roof_state = E_RoofState.closing THEN
	fbComm.Publish('telescope', 'dome', 'AUXILIARY.DOME.REALPOS', '0.3');
ELSIF roof_state = E_RoofState.opening THEN
	fbComm.Publish('telescope', 'dome', 'AUXILIARY.DOME.REALPOS', '0.7');
ELSIF roof_state = E_RoofState.stopped THEN
	fbComm.Publish('telescope', 'dome', 'AUXILIARY.DOME.REALPOS', '0.5');
ELSIF roof_state = E_RoofState.closed THEN
	fbComm.Publish('telescope', 'dome', 'AUXILIARY.DOME.REALPOS', '0.0');
ELSIF roof_state = E_RoofState.error THEN
	fbComm.Publish('telescope', 'dome', 'AUXILIARY.DOME.REALPOS', '-1.0');
ELSE
	fbComm.Publish('telescope', 'dome', 'AUXILIARY.DOME.REALPOS', '-0.5');
END_IF

IF roof_state = E_RoofState.opened OR roof_state = E_RoofState.opening THEN
	fbComm.Publish('telescope', 'dome', 'AUXILIARY.DOME.TARGETPOS', '1.0');
ELSE
	fbComm.Publish('telescope', 'dome', 'AUXILIARY.DOME.TARGETPOS', '0.0');
END_IF

IF roof_state = E_RoofState.error THEN
	fbComm.Publish('telescope', 'dome', 'AUXILIARY.DOME.ERROR_STATE', '1');
	fbComm.Publish('telescope', 'dome', 'AUXILIARY.DOME.READY_STATE', '-1.0');
ELSE
	fbComm.Publish('telescope', 'dome', 'AUXILIARY.DOME.ERROR_STATE', '0');
	IF roof_state = E_RoofState.closed THEN
		fbComm.Publish('telescope', 'dome', 'AUXILIARY.DOME.READY_STATE', '0.0');
	ELSE
		fbComm.Publish('telescope', 'dome', 'AUXILIARY.DOME.READY_STATE', '1.0');
	END_IF
END_IF

IF state = E_RoofState.opening OR state = E_RoofState.closing THEN
	fbComm.Publish('telescope', 'dome', 'AUXILIARY.DOME.MOTION_STATE', '1.0');
ELSE
	fbComm.Publish('telescope', 'dome', 'AUXILIARY.DOME.MOTION_STATE', '0.0');
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Close" Id="{09e5ef83-053e-434a-9daf-0f46715c9ca8}">
      <Declaration><![CDATA[METHOD Close : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR i := 1 TO 2 DO
	roofs[i].Close();
END_FOR;]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_Init" Id="{dd850902-58ca-498a-b6a3-06589bee5a0c}">
      <Declaration><![CDATA[//FB_Init is always available implicitly and it is used primarily for initialization.
//The return value is not evaluated. For a specific influence, you can also declare the
//methods explicitly and provide additional code there with the standard initialization
//code. You can evaluate the return value.
METHOD FB_Init: BOOL
VAR_INPUT
    bInitRetains	: BOOL; // TRUE: the retain variables are initialized (reset warm / reset cold)
    bInCopyCode		: BOOL;  // TRUE: the instance will be copied to the copy code afterward (online change)
	comm			: I_Comm;   
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.fbComm := comm;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Open" Id="{158f37cb-bd12-4fa4-ba18-0d91b517278d}">
      <Declaration><![CDATA[METHOD Open : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR i := 1 TO 2 DO
	roofs[i].Open();
END_FOR;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Reset" Id="{e643a7f5-953f-43e5-b293-e4f6fb37cbd6}">
      <Declaration><![CDATA[METHOD Reset : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR i := 1 TO 2 DO
	roofs[i].Reset();
END_FOR;]]></ST>
      </Implementation>
    </Method>
    <Property Name="State" Id="{7611ff11-7d75-4e77-a763-7a45ea36a762}">
      <Declaration><![CDATA[PROPERTY State : E_RoofState]]></Declaration>
      <Get Name="Get" Id="{63bdd3e1-4e74-409f-b6c0-a0531e107c7a}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[State := roof_state;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Stop" Id="{b18a1fc2-196a-46c9-9a0b-0d399c89b25a}">
      <Declaration><![CDATA[METHOD Stop : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR i := 1 TO 2 DO
	roofs[i].Stop();
END_FOR;]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_RoofControl">
      <LineId Id="55" Count="1" />
      <LineId Id="316" Count="0" />
      <LineId Id="318" Count="2" />
      <LineId Id="317" Count="0" />
      <LineId Id="208" Count="0" />
      <LineId Id="202" Count="3" />
      <LineId Id="201" Count="0" />
      <LineId Id="207" Count="0" />
      <LineId Id="206" Count="0" />
      <LineId Id="254" Count="0" />
      <LineId Id="360" Count="0" />
      <LineId Id="364" Count="0" />
      <LineId Id="257" Count="0" />
      <LineId Id="255" Count="1" />
      <LineId Id="57" Count="0" />
      <LineId Id="260" Count="0" />
      <LineId Id="363" Count="0" />
      <LineId Id="311" Count="0" />
      <LineId Id="258" Count="0" />
      <LineId Id="58" Count="18" />
      <LineId Id="270" Count="0" />
      <LineId Id="411" Count="4" />
      <LineId Id="272" Count="1" />
    </LineIds>
    <LineIds Name="FB_RoofControl._SendTelemetry">
      <LineId Id="12" Count="0" />
      <LineId Id="15" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="54" Count="0" />
      <LineId Id="52" Count="0" />
      <LineId Id="55" Count="0" />
      <LineId Id="53" Count="0" />
      <LineId Id="18" Count="2" />
      <LineId Id="57" Count="1" />
      <LineId Id="21" Count="1" />
      <LineId Id="16" Count="0" />
      <LineId Id="24" Count="0" />
      <LineId Id="23" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="27" Count="1" />
      <LineId Id="26" Count="0" />
      <LineId Id="29" Count="0" />
      <LineId Id="13" Count="0" />
      <LineId Id="30" Count="0" />
      <LineId Id="35" Count="0" />
      <LineId Id="32" Count="1" />
      <LineId Id="36" Count="1" />
      <LineId Id="42" Count="1" />
      <LineId Id="38" Count="0" />
      <LineId Id="31" Count="0" />
      <LineId Id="44" Count="0" />
      <LineId Id="41" Count="0" />
      <LineId Id="45" Count="0" />
      <LineId Id="47" Count="1" />
      <LineId Id="46" Count="0" />
      <LineId Id="11" Count="0" />
    </LineIds>
    <LineIds Name="FB_RoofControl.Close">
      <LineId Id="10" Count="1" />
      <LineId Id="6" Count="0" />
    </LineIds>
    <LineIds Name="FB_RoofControl.FB_Init">
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_RoofControl.Open">
      <LineId Id="11" Count="1" />
      <LineId Id="10" Count="0" />
    </LineIds>
    <LineIds Name="FB_RoofControl.Reset">
      <LineId Id="13" Count="1" />
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_RoofControl.State.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_RoofControl.Stop">
      <LineId Id="10" Count="1" />
      <LineId Id="6" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>