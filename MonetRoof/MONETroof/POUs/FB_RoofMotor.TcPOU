<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_RoofMotor" Id="{47cefcf1-8ba0-4c06-bdcd-b7d169ce9c61}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_RoofMotor
VAR_INPUT
	max_speed				: INT;
	acceleration			: UINT;	
	min_position			: INT;
	max_position			: INT;
	comm					: I_Comm;
END_VAR
VAR_OUTPUT
	// position
	position				: INT;
	percent_open			: REAL;
	
	// moving
	moving					: BOOL;
	
	// real speed
	real_speed				: INT;	
	
	// error 
	error			AT%I*	: BOOL;
END_VAR
VAR
	// signals from limit switches
	opened			AT%I*	: BOOL;
	closed			AT%I*	: BOOL;
	
	// triggers for opened/closed
	opened_trigger			: R_TRIG;
	closed_trigger			: R_TRIG;
	
	// error
	DriveErrorEvent			: FB_EventLog;

	// hall sensor for counting rotation
	counter			AT%I*	: BOOL;
	counter_trig			: R_TRIG;
	
	// reset command to drive
	reset_drive			AT%Q*	: BOOL;
	reset_trigger				: BOOL;
	
	// moving
	direction_open	AT%Q*	: BOOL;
	direction_close	AT%Q*	: BOOL;

	// move speed
	vel_ramp				: FB_Ramp;
	speed			AT%Q*	: INT;
	target_speed			: INT;		
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[// reset?
reset_drive := FALSE;
IF reset_trigger THEN
	reset_drive := TRUE;
	reset_trigger := FALSE;
END_IF

// ramp for roof velocity
vel_ramp(
	start		:= real_speed,
	target		:= target_speed,
	accel		:= acceleration,
	value		=> real_speed);
speed := ABS(real_speed);

// get direction
direction_open := real_speed > 0;
direction_close := real_speed < 0;

// counter for position
counter_trig(CLK := counter);
IF counter_trig.Q THEN
	IF direction_open THEN
		position := position + 1;
	ELSE
		position := position - 1;
	END_IF
END_IF

// if in limits, reset position
closed_trigger(CLK := closed);
IF closed_trigger.Q THEN
	position := min_position;
END_IF
opened_trigger(CLK := opened);
IF opened_trigger.Q THEN
	position := max_position;
END_IF

// relative position
percent_open := 1.0 * (position - min_position) / (max_position - min_position) * 100.0;
	
// stop on limit switches
IF (direction_close AND closed) OR (direction_open AND opened) THEN
	Stop();
END_IF

// set moving to zero, if no movement
moving := real_speed <> 0;

// error
DriveErrorEvent(	
	Comm			:= comm,
	Trigger 		:= error, 
	Level 			:= ADSLOG_MSGTYPE_ERROR,
	FormatString 	:= 'Drive error.');]]></ST>
    </Implementation>
    <Method Name="Close" Id="{40cd4081-a8ac-4d75-aaee-c72d59685ab9}">
      <Declaration><![CDATA[METHOD Close : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT closed THEN
	moving := TRUE;
	target_speed := -max_speed;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="Open" Id="{e1a1847d-b8ba-48c6-8250-57df9d3b1f67}">
      <Declaration><![CDATA[METHOD Open : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT opened THEN
	moving := TRUE;
	target_speed := max_speed;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="Reset" Id="{aa634c66-2ca0-4bfc-8efe-d1b3d9d3132b}">
      <Declaration><![CDATA[METHOD Reset : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[reset_trigger := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="SoftStop" Id="{a2916879-d03b-4d3c-a270-5ed857d57bc6}">
      <Declaration><![CDATA[METHOD SoftStop : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[target_speed 	:= 0;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Stop" Id="{ecb23643-511b-4e32-9b6a-78443c9c1a37}">
      <Declaration><![CDATA[METHOD Stop : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[moving 			:= FALSE;
speed  			:= 0;
real_speed		:= 0;
target_speed 	:= 0;]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_RoofMotor">
      <LineId Id="297" Count="0" />
      <LineId Id="300" Count="0" />
      <LineId Id="299" Count="0" />
      <LineId Id="301" Count="0" />
      <LineId Id="303" Count="0" />
      <LineId Id="302" Count="0" />
      <LineId Id="298" Count="0" />
      <LineId Id="30" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="27" Count="2" />
      <LineId Id="9" Count="0" />
      <LineId Id="95" Count="0" />
      <LineId Id="99" Count="0" />
      <LineId Id="98" Count="0" />
      <LineId Id="100" Count="1" />
      <LineId Id="145" Count="0" />
      <LineId Id="144" Count="0" />
      <LineId Id="146" Count="2" />
      <LineId Id="150" Count="0" />
      <LineId Id="152" Count="1" />
      <LineId Id="151" Count="0" />
      <LineId Id="149" Count="0" />
      <LineId Id="159" Count="0" />
      <LineId Id="158" Count="0" />
      <LineId Id="173" Count="2" />
      <LineId Id="172" Count="0" />
      <LineId Id="170" Count="0" />
      <LineId Id="160" Count="2" />
      <LineId Id="245" Count="0" />
      <LineId Id="244" Count="0" />
      <LineId Id="246" Count="0" />
      <LineId Id="31" Count="2" />
      <LineId Id="50" Count="0" />
      <LineId Id="35" Count="0" />
      <LineId Id="103" Count="0" />
      <LineId Id="102" Count="0" />
      <LineId Id="104" Count="0" />
      <LineId Id="221" Count="0" />
      <LineId Id="220" Count="0" />
      <LineId Id="223" Count="0" />
      <LineId Id="229" Count="0" />
      <LineId Id="224" Count="2" />
    </LineIds>
    <LineIds Name="FB_RoofMotor.Close">
      <LineId Id="13" Count="1" />
      <LineId Id="16" Count="0" />
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_RoofMotor.Open">
      <LineId Id="8" Count="0" />
      <LineId Id="6" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_RoofMotor.Reset">
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_RoofMotor.SoftStop">
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_RoofMotor.Stop">
      <LineId Id="7" Count="0" />
      <LineId Id="10" Count="0" />
      <LineId Id="15" Count="0" />
      <LineId Id="11" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>